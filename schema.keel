model Account {
    fields {
        identity Identity @unique
        name Text
        projects AccountProject[]
    }

    operations {
        create createAccount() with (name) {
            @permission(expression: ctx.isAuthenticated)
            @set(account.identity = ctx.identity)
        }
        get myAccount() {
            @where(account.identity == ctx.identity)
        }
    }

    @permission(
        actions: [get, update, delete],
        expression: ctx.identity == account.identity
    )
}

model AccountProject {
    fields {
        account Account
        project Project
    }

    // Ideally be created as created as part of project creation via a function
    operations {
        create createAccountProject() with (accountId, projectId) {
            @permission(expression: ctx.isAuthenticated)
        }
    }
}

model ProjectInvite {
    fields {
        email Text
        project Project
        code Text
        expiry Timestamp
    }

    functions {
        create createProjectInvite() with (email, projectId) {

        }
    }
}

model Project {
    fields {
        title Text
        accounts AccountProject[]
        //TODO: add allowed URLs list so that we only accept events sent from within that webpage
    }

    operations {
        get getProject(id)
        create createProject() with (title) {
            @permission(expression: ctx.isAuthenticated)
        }
        list listProjects() {
            @where(project.accounts.account.identity == ctx.identity)
        }
        delete deleteProject(id)
        update updateProject(id) with (title)
    }

    @permission(
        actions: [get, list, update, delete],
        expression: project.accounts.account.identity == ctx.identity
    )
}

model AllowedUrl {
    fields {
        url Text
        project Project
    }

    operations {
        create createAllowedUrl() with (url, projectId) {
            @permission(expression: true)
            // @permission(expression: allowedUrl.project.accounts.account.identity == ctx.identity)
        }
        list getAllowedUrls(projectId) {
            @where(allowedUrl.project.accounts.account.identity == ctx.identity)
            @permission(expression: true)
        }
        delete deleteAllowedUrl(id) {
            @permission(expression: true)
        }
    }
}

enum Type {
    Pageview
    Event
}

model TrackingEvent {
    fields {
        name Text
        project Project
        metadata Text?
        host Text?
        // TODO: this should come from the browser. Search for other cool data to add
        userAgent Text?
        // TODO: add this to tracker so it can be provided
        userId Text?
        type Type?
    }

    functions {
        create createTrackingEvent() with (name, metadata, host, projectId, type) {
            @permission(expression: true)
        }
    }
}

enum Chirp {
    Silent
    Click
    Pop
    Tadah
    Woosh
}

model TrackingEventChirp {
    fields {
        eventName Text
        chirp Chirp
        project Project
    }

    operations {
        list getTrackingEventChirp(projectId) {
            @where(trackingEventChirp.project.accounts.account.identity == ctx.identity)
            @permission(expression: true)
        }
        update updateTrackingEventChirp(id) with (chirp)
    }
}

api Tracker {
    @rpc

    models {
        TrackingEvent
    }
}

api Admin {
    @graphql

    models {
        Account
        AccountProject
        Project
        AllowedUrl
        TrackingEvent
        TrackingEventChirp
    }
}
